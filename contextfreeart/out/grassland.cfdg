startshape start
 background { sat 1 b -0.87 hue 260}

rule start {
  2500 * {z 0.0001 x 0.1} halm {}
  625 * {z 0.0001 x 0.4} bottom {y -1.5 b 0.5 z 4}
}
rule bottom {
  30*{y -0.5}
  CIRCLE {y 15 s 3 hue 88 sat 1}

  14*{y -0.5 hue -4 b -0.2}
  CIRCLE {s 3 hue 88 sat 1}
}
rule algen {
  CIRCLE {}
  CIRCLE {z 0.01 s 0.9 b 0.1}
  CIRCLE {z 0.02 s 0.8 b 0.2}
  algen {s 0.99 y 0.3 r 2 hue 0.005|}
}
rule algen {
  CIRCLE {}
  CIRCLE {z 0.01 s 0.9 b 0.1}
  CIRCLE {z 0.02 s 0.8 b 0.2}
  algen {s 0.99 y 0.3 r -2 hue 0.005|}
}
rule algen 0.03 {
  algen {r -20}
  algen {r 20}
}
rule halm {
  manus {s 1.5 z 4 sat 1 hue 90 b 0.5}
}
rule halm {
  manus {s 1.4 z 3 y 10 sat 1 hue 90 b 0.4}
}
rule halm {
  manus {s 1.3 z 2 y 20 sat 1 hue 90 b 0.3}
}
rule halm {
  manus {s 1.2 z 1 y 30 sat 1 hue 90 b 0.2}
}
rule halm {
  manus {s 1.1 y 40 sat 1 hue 90 b 0.1}
}
rule kringel 1 {
  kringell {}
}
rule kringel 1 {
  kringelr {}
}
rule kringell {
  CIRCLE {}
  kringell {r 2 y 0.4 s 0.993}
}
rule kringelr {
  CIRCLE {}
  kringelr {r -2 y 0.4 s 0.993}
}
rule ast 1 {
  CIRCLE {}
  ast  {y 0.7 s 0.996 r -1}
}
rule ast 1 {
  CIRCLE {}
  ast  {y 0.7 s 0.996 r 1}
}
rule ast 0.05 {
  kringel {}
  ast  {}
}
rule ast 0.02 {
  top {s 4 hue -120 |hue 180}
}
rule top {
  CIRCLE {}
  top {r 30 x 0.7 s 0.99 hue 0.01|}
}
rule manus 0.005 {
 ast {}
}
rule manus 0.002 {
 algen {z -0.1 y 2 b -0.7 s 5 hue -50 |hue 170}
}
rule manus 1 {
  manu {}
}
rule manu 1 {
  CIRCLE {}
  manu {y 0.3 s 0.99}
}
rule manu 1 {
  CIRCLE {}
  manu {y 0.3 s 0.99 r 6}
}
rule manu 1 {
  CIRCLE {}
  manu {y 0.3 s 0.99 r -6}
}
rule manu 0.008 {
  manu {s 0.99 r 4 b 0.1}
  manu {s 0.99 r -4 b 0.1}
}
rule manu 0.005 {
  CIRCLE {s 2 z 1 hue -100 b 1}
  CIRCLE {s 1.5 z 1 hue -50 b 1}
}
rule manu 0.005 {
  CIRCLE {s 2 z 1 hue 150 b 1}
  CIRCLE {s 1.2 z 1 hue 230 b 1}
}