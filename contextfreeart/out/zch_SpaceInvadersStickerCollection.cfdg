/*
	Space Invaders Sticker Collection
	by zch 20??-??
	variants: R, S, LOL, OQP, OQY, ORI
*/
startshape START

rule START {
  BG { s 1.1 z-100}
  SOME { sat 0.25 }
}

rule OR2X2 { 2*{x+1} { 2*{y+1} ONE {  x -0.5	y -0.5	} } }
rule OR3X3 { 3*{x+1} { 3*{y+1} ONE {  x -1.0	y -1	} } }  
rule OR4X4 { 4*{x+1} { 4*{y+1} ONE {  x -1.5	y -1.5	} } }
rule OR13 {
  4*{x+1}  { 
    ONE { x-1.5	y-1.5	} 
    ONE { x-1.5	y+1.5	}}
  2*{y+1}  { 
    ONE { x-1.5	y-0.5	} 
    ONE { x+1.5	y-0.5	}}
    ONE { s 2 }    
}

rule R2X2 { 2*{x+1} { 2*{y+1} SOME {  x -0.5	y -0.5	} } }
rule R3X3 { 3*{x+1} { 3*{y+1} SOME {  x -1.0	y -1	} } }  
rule R4X4 { 4*{x+1} { 4*{y+1} SOME {  x -1.5	y -1.5	} } }
rule R13 {
  4*{x+1}  { 
    SOME { x-1.5	y-1.5	} 
    SOME { x-1.5	y+1.5	}}
  2*{y+1}  { 
    SOME { x-1.5	y-0.5	} 
    SOME { x+1.5	y-0.5	}}
    SOME { s 2 }    
}

rule SOME { R2X2 { z+5 s 0.5 } }
rule SOME { R3X3 { z+5 s 0.333333333333 } }
rule SOME { R13 { z+5 s 0.25 } }

rule SOME { BG { } R2X2 { z+5 s (0.9*0.5) } }
rule SOME { BG { } R3X3 { z+5 s (0.9*0.333333333333) } }
rule SOME { BG { } R13 { z+5 s (0.9*0.25) } }

rule SOME { BG { } OR2X2 { z+5 s (0.9*0.5) } }
rule SOME { BG { } OR3X3 { z+5 s (0.9*0.333333333333) } }
rule SOME { BG { } OR4X4 { z+5 s (0.9*0.25) } }
rule SOME { BG { } OR13 { z+5 s (0.9*0.25) } }

rule SOME 10 { ONE {} }

rule SOME 30 { SOME {hue+30} }
rule SOME 5 { SOME {r+1} }
rule SOME 5 { SOME {r-1} }

rule PIXEL { SQUARE { z+1.5 b 0.4 sat 0.5 }  SQUARE { z+1.6 s 0.8 b 0.1 sat 0.5 } }
rule PIXEL { SQUARE { z+1.5 b 0.4 sat 0.5 }  SQUARE { z+1.6 s 0.8 b 0.2 sat 0.5 } }

rule PIXEL 5 { PIXEL { r+1} }
rule PIXEL 5 { PIXEL { r-1} }
rule PIXEL { PIXEL { s 0.95} }
rule PIXEL { PIXEL { s 1.05} }


rule MPIX0 {}
rule MPIX1 {}
rule MPIX2 {}
rule MPIX3 {}
rule MPIX0 { PIXEL [ x-0.5] PIXEL [ x+0.5] }
rule MPIX1 { PIXEL [ x-1.5] PIXEL [ x+1.5]}
rule MPIX2 { PIXEL [ x-2.5] PIXEL [ x+2.5]}
rule MPIX3 { PIXEL [ x-3.5] PIXEL [ x+3.5]}
rule ROW { MPIX0 {} MPIX1 {} MPIX2 {} MPIX3 {} }

rule _INVADER { 8*{y+1} ROW { y-3.5 } }
rule INVADER { _INVADER { s 0.125 } }

rule BG {
  SQUARE { z+0.1 sat -1  b 1 }
  SQUARE { z+0.2 s 0.9 hue+90 b 0.9 }
}

rule BG {
  SQUARE { z+0.1 sat -1  b 1 }
  SQUARE { z+0.2 s 0.9 hue+90 b 0.9 }
  CIRCLE { z+0.3 s 0.9 b 1 sat -1 a -0.5}
}

rule BG { BG { hue + 5 } }

rule ONE { ONE { r-1 } }
rule ONE { ONE { r+1 } }
rule ONE { ONE { hue+90 } }


rule ONE { INVADER { s 0.75 } }
rule ONE 0.5 { BG { s 0.75 sat -1 } INVADER { s 0.6 } }
rule ONE 0.5 { BG { s 0.75 sat 0.5 } INVADER { s 0.6 } }
