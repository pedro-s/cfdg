startshape plant{color alpha}

rule plant {
   rose { x 2 y 1 b 0.6  sat 1 hue=0 }
   stem {  s 1.2 1.8 x 1.7 y -1.2 }

   rose { x -1 r 180 b 0.6 sat 1}
   stem { flip 90  s 1.2 y -2 }

   leaf { skew 0 -25 r 75 x 0.4 y -2.2 b =0.5 hue =120 sat 0.6 }
   leaf { s 1 0.8  flip 90 r -70 x 1.15 y -1.8 b =0.5 hue =120 sat 0.6 }
}

rule rose {
	rosestart { }
	rosestart { r 60 }
	rosestart { r 120 }
	rosestart { r 180 }
}

rule rosestart {
   petal { s 1.8  }
   rosepart { r -25 s 0.7  }
}

rule rosepart {
   petal { s 2 }
   rosepart { r -25 s 0.9  }
}

rule petal {
   CIRCLE { s 0.9 1.8}
   CIRCLE { s 0.85 1.7 b 0.2   }
   CIRCLE { s 0.8 1.6  b 0.4  }
   petal { r -105 y 0.04 x 0.04 size 0.9 h 0.8 b 0.1  }
}

rule stem { 
    CIRCLE {  b =0.5 hue =120 r -25 s 1 3  sat 0.6 }
    CIRCLE { r -25 x 0.12 y -0.03 s 1 3 b 1 sat -1 }
    SQUARE {  x -0.7 y -1.2 s 0.5 b 1   }  
}

rule leaf {
	SQUARE [s 0.1 0.6 ]
      leafpart { r 5 y 0.8 x -0.05 }
}

rule leafpart {
	CIRCLE { b =0.4 }
	CIRCLE { s 0.9 b 0.6 }
      SQUARE { s 0.1 1 }
	leafpart { r 1.5 y 0.1 s 0.9 }
}



