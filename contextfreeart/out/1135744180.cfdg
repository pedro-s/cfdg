startshape init
background{b -0.8 hue 200 sat 0.6}

rule init
{
 crawly{x -100 r -20 y 50 hue 215 sat .6 alpha -0.5}
 crawly{x 100 r 170 hue 200 sat 0.4 alpha -0.5}
}

rule init
{
 crawly{x -90 y -40 r 45 hue 215 sat .6 alpha -0.5}
 crawly{x 100 r 190 hue 200 sat 0.4 alpha -0.5}
}

rule init
{
 crawly{x -9 y -40 r 99 hue 215 sat .6 alpha -0.5}
 crawly{y 120 r -99 hue 200 sat 0.4 alpha -0.5}
}

rule crawly
{
 body{}
 crawler {b 1}
 crawler {r 180 b 1}
}

rule body
{
 CIRCLE{s 60 b 0.5 alpha -0.3}
 body{s 0.8}
}

rule crawler
{
 squiggle{alpha -0.7}
 crawler{r 2 x 0.1 s 0.9 b -0.005}
}

rule crawler
{
 squiggle {alpha -0.7}
 crawler{r 4 x 0.1 s 0.9 b -0.005}
}

rule crawler
{
 squiggle {alpha -0.7}
 crawler{r -2 x 0.1 s 0.9 b -0.005}
}

rule crawler
{
 squiggle {alpha -0.7}
 crawler{r 4 x 0.1 s 0.9 b -0.005}
}

rule crawler 0.05
{
 crawler{r 40 alpha -0.2}
 crawler{r -40 alpha -0.2}
}

rule crawler 0.05
{
 crawler{r 60 alpha -0.2}
 crawler{r -60 alpha -0.2}
}

rule squiggle
{
 CIRCLE{s 5}
 segment{s 1 20 hue 20 sat 0.8 b 0.6}
 squiggle{y 0.9 r 8}
}
rule squiggle
{
 CIRCLE{s 5}
 segment{s 1 20 hue 20 sat 0.8 b 0.6}
 squiggle{y 0.9 r -5}
}

rule squiggle 0.5
{
 squiggle{flip 60 alpha -0.01}
}

rule segment{
CIRCLE {alpha -0.5}
}

rule squiggle 0.005 {}