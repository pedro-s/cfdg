startshape START 
background {hue 50 sat .2 b 1} 

rule START {
	BRANCH { hue 20 sat 0.8 b 0.8 }
	BRANCH { flip 90 hue 20 sat 0.8 b 0.8 }
}

rule BRANCH {
	SQUARE { }
	BRANCH { y 1 s 0.99 r 0.6 }
}

rule BRANCH 0.03 {
	BRANCH { flip 90 }
}

rule BRANCH 0.01 {
	BRANCH { r 15 s 0.99 }
	BRANCH { r -15 s 0.99 }
}

rule BRANCH 0.01 {
	FULL_FLOWER { s 0.5 }
}

rule FULL_FLOWER {
	FLOWER { hue 10 sat 0.8 b 0.8}
	FULL_FLOWER { s 0.7 r 27.8 hue 5 alpha -0.1}
}

rule CURVE_I {
	SHAPE { }
	CURVE_I { y 1 r 1.5 s 0.99 alpha -0.01}
}

rule CURVE_D {
	SHAPE { }
	CURVE_D { y 1 r -1.5 s 0.99 alpha -0.01}
}

rule FLOWER {
	CURVE_I { }
	CURVE_I { r 90 }
	CURVE_I { r 180 }
	CURVE_I { r 270 }
	
	CURVE_D { }
	CURVE_D { r 90 }
	CURVE_D { r 180 }
	CURVE_D { r 270 }
}

rule SHAPE { SQUARE { } }
rule SHAPE 0.01 { 
	SQUARE { }	
	CIRCLE { s 2 6 alpha -0.2 }
}

rule SHAPE 0.01 {
	SQUARE { }
	CURVE_I { s 0.5 }
}