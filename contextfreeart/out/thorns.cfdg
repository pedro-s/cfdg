/*
	THORNS
	www.lazymoon.org
	thorns.cfdg
*/

startshape I
background {b -0.9}
rule I {
	16*[r 45] TWIRL[h 222 |h 100 sat 0.7 |sat 0.8 b 0.8 ]
}

rule TWIRL 45{
	SY[]
	TWIRL[x 1 s 0.99 h -0.01| sat -0.01|]
}
rule TWIRL 5{
	TURNL[]
}
rule TWIRL 5{
	TURNR[]
}




rule TURNL 95{
	SY[]
	TURNL[x 1 r 5]
}
rule TURNL 30{
	TURNL[]
	L[r -90]
}
rule TURNL 5{
	E[]
}




rule TURNR 95{
	SY[]
	TURNR[x 1 r -5]
}
rule TURNR 5{
	E[]
}
rule TURNR 30{
	TURNR[]
	L[r 90 sat -1 sat 0|]
}



rule E {
	C[a -0.85 s 0.5]
	TWIRL[]
}
rule L {
	SQUARE[a -0.25]
	L[x 1 s 0.9 sat -0.1|]
}



rule C{CIRCLE[s 10]}
rule C{CIRCLE[s 20]}
rule C{CIRCLE[s 30]}

rule SY{CIRCLE[a -0.5]S[y -1]}
rule SY{CIRCLE[a -0.5]S[y -2]}
rule SY{CIRCLE[a -0.5]S[y 1]}
rule SY{CIRCLE[a -0.5]S[y 2]}

rule S {SQUARE[s 1 0.5]}
rule S {SQUARE[s 1 0.8]}
rule S {SQUARE[s 1 0.6]}
rule S {SQUARE[s 1 0.4]}
rule S {SQUARE[s 1 0.2]}