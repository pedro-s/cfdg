startshape Canvas // Width, Height: 800*800 pixels
CF::Background=[b -1]
CF::Impure=1
CF::Size=[s 8 8]

Ori=0,1,0,1,0,1,0,1,0,0,1,0,1,0,1,0
Loc(X)=1+2*floor(max(-3,min(X,3))/2)
Rad(X,Y)=let
( U=X-Loc(X) ; V=Y-Loc(Y)
; C=if(Ori[floor(8-2*Loc(Y)+Loc(X)/2)]
      ,if(U+V>0,(+1,+1),(-1,-1))
      ,if(U-V>0,(+1,-1),(-1,+1)))
; sqrt((U-C[0])^2+(V-C[1])^2)
)

shape Canvas
{ loop  X=-3.9975,4.0025,0.005 []
   loop Y=-3.9975,4.0025,0.005 []
    Point(0,X,Y)[x X Y s 0.01]
}

shape Point(natural i,X,Y)
{ if(i<7) { R=4*(0.25-abs(1-Rad(X,Y)))
   if (R<0) { Next(i,X,Y)[h 30 a -0.3]
      SQUARE[h -30 sat 0.5 b -1 a -0.9 z (0.8^i)]
   } else if (R<0.1) { SQUARE[sat -1 b -1  a -0.1]
   } else {   Next(i,X,Y)[h -5 sat 1 b .25 a -0.2]
      SQUARE[b (R^(1/2)) a -0.75 z (0.9^i)]
} }}

shape Next(natural i,X,Y)
{ if (Y>0) if (X>0) Point(i+1, 2*(X-2), 2*(Y-2))[]
           else     Point(i+1, 2*(Y-2),-2*(X+2))[]
  else     if (X>0) Point(i+1,-2*(X-2),-2*(Y+2))[]
           else     Point(i+1, 2*(X+2), 2*(Y+2))[]
}
