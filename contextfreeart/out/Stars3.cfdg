
startshape all

background { b -1 }
tile { s 12 }

rule all {
stars { b 1}
cloud {}

}




rule cloud_yes_no {
	cloud {}
}

rule cloud_yes_no {}

rule cloud { cloud_color {} }


rule cloud_color { cloud_stars { hue 271.64} }



rule cloud_stars {
3000 * { b 0.001 sat 0.001}
 star_angle {  }
}


rule star_angle 1   { star_angle { r  13 } }
rule star_angle 10  { star_angle { r  1.3 } }
rule star_angle 0.1 { star_angle { r  139 } }

rule star_angle 0.1 { star_pos {} }

rule star_pos    { star_pos { x  .14 } }
rule star_pos .5 { star_pos { x  1.4 } }
rule star_pos .5 { star_pos { x  2.4 } }


rule star_pos  { star_pos_micro {} }

rule star_pos_micro 4  { star_pos_micro { x -.11 } }
rule star_pos_micro 4  { star_pos_micro { x .11 } }
rule star_pos_micro 4  { star_pos_micro { y  .11 } }
rule star_pos_micro 4  { star_pos_micro { y -.11 } }
rule star_pos_micro    { star_color {} }



rule star_color { star_color { s 1 } }

rule star_color 20 { star_color { sat -0.01 } }
rule star_color 20 { star_color { hue  3.1  } }
rule star_color    { star_color { b   -0.05 } }

rule star_color {
  star_size {}
}




rule star_size { star_paint { s 0.011 } }
rule star_size { star_paint { s 0.015 } }
rule star_size { star_paint { s 0.009 } }
rule star_size { star_paint { s 0.008 } }
rule star_size { star_paint { s 0.01   } }
rule star_size { star_paint { s 0.013 } }

rule star_paint { 
CIRCLE { sat -0.9}
star_paint_glow { alpha -0.995  }
cloud_paint_glow { alpha -0.99 }
}

rule star_paint_glow {
10 * { s 1.01 alpha 0.001 } CIRCLE { s 5  }

}

rule cloud_paint_glow { 
10 * { s 1.2 alpha -0.01}
CIRCLE { s 60 sat - .4 b .1 hue 280} 

}


//---------------------------------------
//         big star
//---------------------------------------

rule stars {
 10* { x 1 } { 
 10* { y 1 }  
 big_star_yes_no { alpha 1 sat -1 s 0.01}  } 
}

rule big_star_yes_no {
}

rule big_star_yes_no .1 { big_star_pos {} }

rule big_star_pos 15{	big_star_pos { x  1.1 } }
rule big_star_pos 15{	big_star_pos { y  1.1 } }
rule big_star_pos 15{	big_star_pos { x -1.1 } }
rule big_star_pos 15{	big_star_pos { y -1.1 } }
rule big_star_pos { big_star_size { } }

rule big_star_size { big_star_sat {s  0.8 } }
rule big_star_size { big_star_sat {s  1.2 } }
rule big_star_size { big_star_sat {s  0.3 } }
rule big_star_size { big_star_sat {s  2.0 } }

rule big_star_sat { big_star_hue { sat 0.2|  } }
rule big_star_sat { big_star_hue { sat -0| } }
rule big_star_sat { big_star_hue { sat -0.2| } }
rule big_star_sat { big_star_hue { sat -0.1| } }

rule big_star_hue { big_star { hue 201 } }
rule big_star_hue { big_star { hue 271 } }
rule big_star_hue { big_star { hue 241 } }
rule big_star_hue { big_star { hue 301 } }

rule big_star {
30 * 
	{s 1.1 alpha -0.1}
   CIRCLE {s 1 alpha -0.95}
50 * 
  {s 1.1 alpha -0.001  }
   big_star_cross  { alpha -0.985 }
	CIRCLE { s 10 alpha -0.95}
}

path big_star_cross {
	MOVETO {x 1 y 0}
	ARCTO { x 0 y 1 rx 1.2 ry 1 r 45 p cw }
	ARCTO { x -1 y 0 rx 1.2 ry 1 r 135 p cw }
	ARCTO { x 0 y -1 rx 1.2 ry 1 r 225 p cw }	
	ARCTO { x 1 y 0 rx 1.2 ry 1 r 315 p cw}

}




