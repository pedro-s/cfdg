CF::Background = [hue 180 sat 0.25 b -0.1]
CF::Impure = 1

startshape Canvas[sat 0.75 b 1]
N=25

shape Canvas
{ 
  loop A=0,360*200,1 []
    loop Z=200,0,-0.1 []
      loop R=Z*0.9,Z,(Z/N) []
        if(rand(50000)<(Z+1))
          Practicle(R/4, A, Z, 0.5) []
}

shape Practicle(R, A, Z, S)
{
  X = R*cos(A) + 0.1*Z*cos(5*Z)
  Y = R*sin(A) + 0.1*Z*sin(5*Z)
  H = 30 + 3.1*sin(A*10+50+50*sin(A*10))+3.1*sin(Z*15)+0.05*Z
  Point(X, Y, Z) [s S h H b (0.25+7.5*(4*R/Z-1)) ]
}

shape Point(X, Y, Z) 
{
  Xcoord = X + Y * 0.5
  Ycoord = Y * 0.5 + Z
  S = 1 //(rand(100)/50)
  Point2 [
    s S
    x Xcoord y Ycoord 
    a -0.25 b ((Z+100)/800+0.2) 
    z (-Y*100+S)
  ]
}
 
shape Point2 
{
  CIRCLE [s 1.25 b -0.3521 a -0.432]
  CIRCLE [s 1 z 1]
}