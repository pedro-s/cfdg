startshape HYPERSIGIL
//include holywords.cfdg
//include goetic.cfdg
include i_polygons.cfdg
include i_pix.cfdg

rule DATA{
SQUARE{s .1 1}
}
rule DATA{
SQUARE{s .3 1}
}
rule DATA{
SQUARE{s .1 1.25}
}
rule DATA .5{}

rule SIGILX{
40* {r -2.5} DATA[y .75 s .05]
150* {r -1} CIRCLE[r 10 y .75 s .01]
CIRCLE[r 10 y .75 s .03]
}


rule SIGILX 5 {}

rule SIGILX{
3* {r 120} ARC{s .01 y .7 z -1}
}

rule ARC 3{}
rule ARC{
CIRCLE{s 2}
50* {r .5 y .75} CIRCLE{}
TERMINUS[r 12 y 37 s .9 r 15]
}
rule ARC{
CIRCLE{s 2}
50* {r -.5 y .75} CIRCLE{}
TERMINUS[flip 90 r 12 y 37 s .9 r 15]
}

rule TERMINUS .1{
CIRCLE{ s 80 b 1 }
HYPERSIGIL{s 100 }
}
rule TERMINUS .05{
CIRCLE{ s 80 b 1 }
HYPERSIGIL{s 120 }
}
rule TERMINUS{
50* {r .5 y .75} CIRCLE{}
TERMINUS[r 12 y 37 s .9 r 15]
}

rule SIGILX{
CIRCLE{s 1.5 z -2}
CIRCLE{s 1.49 z -2 b 1}
CIRCLE{s 1.1 z -2}
CIRCLE{s 1.09 z -2 b 1}
}
rule SIGILX{
CIRCLE{ y .7 s .03}
200* {r .5} CIRCLE{ y .7 s .01}
}

rule HYPERSIGIL{
SIGIL{}
SIGILX{}
SIGILX{r 45}
SIGILX{r 180}
SIGILX{s 1.2}
SIGILX{s 1.3 r -32}
}
rule HYPERSIGIL .1{
SIGIL{}
CHAOTE{x .3 y -.5 s .1}
SIGILX{}
SIGILX{r 45}
SIGILX{r 180}
SIGILX{s 1.2}
SIGILX{s 1.3 r -32}
}
rule HYPERSIGIL .3{
PRIMELINE{x .3 y -.5 z -1 s .005}
SIGIL{}
SIGILX{}
SIGILX{r 45}
SIGILX{r 180}
SIGILX{s 1.2}
SIGILX{s 1.3 r -32}
}

rule SIGIL {
CIRCLE { }
CIRCLE { s .9 b 1}
CIRCX { z +1 }
}


// circle embellishment rules

rule CIRCX {

SIGIL {s .85 b -1}
}
rule CIRCX {
CIRCLE { s .87 }
CIRCLE { s .85 b 1}
HOLYWORD{ s .055 y .38}
HOLYWORDB{ s .055 y -.38}
SIGIL {s .7 }
SCROSS { x .39 s .15}
SCROSS { x -.39 s .15}
}
rule CIRCX {
90* {r 4} SQUARE { s .01 .05 y .6}
SIGIL {s .8}
}
rule CIRCX {
CIRCLE { s .8}
CIRCLE { s .75 b 1}
18* {r 20} SQUARE { s .01 .08 y .42}
18* {r 20} HLETTER{ s .03 y .42 x .07 r -7}
SIGIL {s .7}
}
rule CIRCX .5 {
CIRCLE { s .8}
CIRCLE { s .75 b 1}
18* {r 20} SQUARE { s .01 .08 y .41 b 1}
SIGIL {s .7}
}
rule CIRCX {
TRIANGLE{s .8}
TRIANGLE{s .75 b 1}
SCROSS{ y .15 x .3 s .3}
SCROSS{ y .15 x -.3 s .3}
SCROSS{ y -.33 s .3}
SIGIL {s .45}
}
rule CIRCX {
CIRCLE { s .98 b 1}
polygon7sided{s .98}
polygon7sided{s .97 b 1}
polygon7sided{s .83 }
polygon7sided{s .82 b 1}
SIGIL {s .74}
7* {r 51} ANGEL[r 13 y .41 s .033]
7* {r 51} SCROSS[r -12 y .45 s .1]
}
rule CIRCX {
PENTAGRAM{ r 17}
// disable comment for inverted pentagrams
// PENTAGRAM{ r 197} 
SIGIL {s .3}
HLETTER {s .1 y .27 x .2}
HLETTER {s .1 y .27 x -.2}
HLETTER {s .1 y -.12 x .32}
HLETTER {s .1 y -.12 x -.32}
HLETTER {s .1 y -.35 }
}
rule PENTAGRAM {
5* {r 72} SQUARE{s .02 .9 y .13 x .06 r 72}
}

rule SCROSS{
SQUARE{ s .3 .05}
SQUARE{ s .05 .3}
TRIANGLE{ s .15 y .15 r 180}
TRIANGLE{ s .15 x .15 r 90}
TRIANGLE{ s .15 y -.15 }
TRIANGLE{ s .15 x -.15 r -90}
}



///
// goetic.cfdg
///

rule PRIMELINE .5{
40* {x+.25} CIRCLE{s 2}
PRIMELINE { x 10}
}
rule PRIMELINE {
40* {x+.25} CIRCLE{s 2}
LINE { x 10  r 90}
PRIMELINE { x 10}
}
rule PRIMELINE .1{
40* {x+.25} CIRCLE{s 2}
CIRCLE{s 5 x 10}
CIRCLE{s 2 x 10 b 1}
}
rule PRIMELINE {
40* {x+.25} CIRCLE{s 2}
LINE { x 10 r -90 flip 180 }
PRIMELINE { x 10}
}
rule PRIMELINE .1{
30* {x+.5 r 3} CIRCLE{s 2}
PRIMELINE { x 9.8 y 9.5 r 90}
}

rule IOTRI{ TRIANGLE{} }
rule IOTRI{ TRIANGLE{r 180} }


// line rules

rule LINE{
40* {x+.25} CIRCLE{s 2}
LINE { x 10} 
}

rule LINE {
40* {x+.25} CIRCLE{s 2}
CIRCLE{s 5 x 10}
CIRCLE{s 2 x 10 b 1}
}
rule LINE {
40* {x+.25} CIRCLE{s 2}
IOTRI{x 10 s 5 r 90}
}

rule LINE .5{
40* {x+.25} CIRCLE{s 2}
DIAGLINE { x 10 r -45}
LINE { x 10}
}
rule LINE .25{
40* {x+.25} CIRCLE{s 2}
DIAGLINE { x 10 r -45}
DIAGLINE { x 10 r -45 flip 0}
LINE { x 10}
}
rule LINE .5{
CROSS{ x 10 }
40* {x+.25} CIRCLE{s 2}
LINE { x 10}
}


// cross bar rules

rule CROSS{
TRIANGLE{y 3 r 180 s 5}
5* {y -1} SQUARE{y 2.5 s 2}
CROSSBAR {y -3}
}
rule CROSSBAR{

}
rule CROSSBAR{
5* {y -1} SQUARE{s 2}
CROSSBAR {y -5}
}
rule CROSSBAR{
20* {y -1} SQUARE{s 2}
CROSSBAR {y -20}
LINE{y -10 x -5}
IOTRI{r 90 s 5 y -10 x -5}
}
rule SCROSS{
SQUARE{ s .3 .05}
SQUARE{ s .05 .3}
TRIANGLE{ s .15 y .15 r 180}
TRIANGLE{ s .15 x .15 r 90}
TRIANGLE{ s .15 y -.15 }
TRIANGLE{ s .15 x -.15 r -90}
}


// diagline

rule DIAGLINE {
40* {x+.25} CIRCLE{s 2}
DIAGLINE { x 10} 
}
rule DIAGLINE {
40* {x+.25} CIRCLE{s 2}
LINE { x 10 r 45 } 
}
rule DIAGLINE {
40* {x+.25} CIRCLE{s 2}
LINE { x 10 } 
}
rule DIAGLINE {
40* {x+.25} CIRCLE{s 2}
}
rule DIAGLINE .05 {
160* {x+.25} CIRCLE{s 2}
HOLYWORD{y 5 s 5 x 10}
DIAGLINE { x 40} 
}


// 
// holywords.cfdg
//

startshape ANGEL
include i_pix.cfdg

// 
// lettering rules
//
rule HOLYWORD{ ELOHIM{}}
rule HOLYWORD{ YHVH{}}
rule HOLYWORD{ DEUS{}}
rule HOLYWORDB{ ELOHIMB{}}
rule HOLYWORDB{ YHVHB{}}
rule HOLYWORDB{ DEUSB{}}

rule ANGEL{
R_5by5{x -3.6 }
A_5by5{x -2.4 }
P_5by5{x -1.2 }
H_5by5{x 0}
A_5by5{x 1.2 }
E_5by5{x 2.4 }
L_5by5{x 3.6 }
}
rule ANGEL{
B_5by5{x -3.6 }
A_5by5{x -2.4 }
R_5by5{x -1.2 }
D_5by5{x 0}
I_5by5{x 1.2 }
E_5by5{x 2.4 }
L_5by5{x 3.6 }
}
rule ANGEL{
G_5by5{x -3.6 }
A_5by5{x -2.4 }
B_5by5{x -1.2 }
R_5by5{x 0}
I_5by5{x 1.2 }
E_5by5{x 2.4 }
L_5by5{x 3.6 }
}
rule ANGEL{
S_5by5{x -3.6 }
A_5by5{x -2.4 }
C_5by5{x -1.2 }
H_5by5{x 0}
I_5by5{x 1.2 }
E_5by5{x 2.4 }
L_5by5{x 3.6 }
}
rule ANGEL{
S_5by5{x -3 }
A_5by5{x -1.8 }
M_5by5{x -.6 }
A_5by5{x .6 }
E_5by5{x 1.8 }
L_5by5{x 3 }
}
rule ANGEL{
R_5by5{x -3 }
A_5by5{x -1.8 }
M_5by5{x -.6 }
I_5by5{x .6 }
E_5by5{x 1.8 }
L_5by5{x 3 }
}
rule ANGEL{
M_5by5{x -4.2 }
E_5by5{x -3 }
T_5by5{x -1.8 }
A_5by5{x -.6 }
T_5by5{x .6 }
R_5by5{x 1.8 }
O_5by5{x 3 }
N_5by5{x 4.2 }
}
rule ANGEL{
S_5by5{x -4.2 }
H_5by5{x -3 }
A_5by5{x -1.8 }
M_5by5{x -.6 }
S_5by5{x .6 }
H_5by5{x 1.8 }
E_5by5{x 3 }
L_5by5{x 4.2 }
}

rule CHAOTE{
H_5by5{}
Y_5by5{x 1.2 }
P_5by5{x 2.4}
E_5by5{x 3.6}
R_5by5{x 4.8}
CONTR{x 6.0}
}
rule CHAOTE{
C_5by5{}
Y_5by5{x 1.2 }
B_5by5{x 2.4}
E_5by5{x 3.6}
R_5by5{x 4.8}
CONTR{x 6.0}
}
rule CHAOTE{
N_5by5{}
E_5by5{x 1.2 }
O_5by5{x 2.4}
CONTR{x 3.6}
}

rule CONTR{
S_5by5{}
I_5by5{x 1.2 }
G_5by5{x 2.4}
I_5by5{x 3.6}
L_5by5{x 4.8}
}
rule CONTR{
G_5by5{}
N_5by5{x 1.2 }
O_5by5{x 2.4}
S_5by5{x 3.6}
T_5by5{x 4.8}
I_5by5{x 6.0}
C_5by5{x 7.2}
}
rule CONTR{
C_5by5{}
O_5by5{x 1.2 }
S_5by5{x 2.4}
M_5by5{x 3.6}
}

rule ELOHIM{
E_5by5{x -3.1 y -.7 r 35}
L_5by5{x -1.8 y -.15 r 20}
O_5by5{x -.6 r 5 y .1}
H_5by5{x .6 r -5 y .1}
I_5by5{x 1.8 y -.15 r -20}
M_5by5{x 3.1 y -.7 r -35}
}
rule YHVH{
Y_5by5{x -1.8 y -.15 r 20}
H_5by5{x -.6 r 5 y .1}
V_5by5{x .6 r -5 y .1}
H_5by5{x 1.8 y -.15 r -20}
}
rule DEUS{
D_5by5{x -1.8 y -.15 r 20}
E_5by5{x -.6 r 5 y .1}
U_5by5{x .6 r -5 y .1}
S_5by5{x 1.8 y -.15 r -20}
}
rule ELOHIMB{
E_5by5{x -3 y .7 r -35}
L_5by5{x -1.8 y .15 r -20}
O_5by5{x -.6 r -5 y -.1}
H_5by5{x .6 r 5 y -.1}
I_5by5{x 1.8 y .15 r 20}
M_5by5{x 3 y .7 r 35}
}
rule YHVHB{
Y_5by5{x -1.8 y .15 r -20}
H_5by5{x -.6 r -5 y -.1}
V_5by5{x .6 r 5 y -.1}
H_5by5{x 1.8 y .15 r 20}
}
rule DEUSB{
D_5by5{x -1.8 y .15 r -20}
E_5by5{x -.6 r -5 y -.1}
U_5by5{x .6 r 5 y -.1}
S_5by5{x 1.8 y .15 r 20}
}

rule HLETTER{A_5by5{}}
rule HLETTER{B_5by5{}}
rule HLETTER{C_5by5{}}
rule HLETTER{D_5by5{}}
rule HLETTER{E_5by5{}}
rule HLETTER{F_5by5{}}
rule HLETTER{G_5by5{}}
rule HLETTER{H_5by5{}}
rule HLETTER{I_5by5{}}
rule HLETTER{J_5by5{}}
rule HLETTER{K_5by5{}}
rule HLETTER{L_5by5{}}
rule HLETTER{M_5by5{}}
rule HLETTER{N_5by5{}}
rule HLETTER{O_5by5{}}
rule HLETTER{P_5by5{}}
rule HLETTER{Q_5by5{}}
rule HLETTER{R_5by5{}}
rule HLETTER{S_5by5{}}
rule HLETTER{T_5by5{}}
rule HLETTER{U_5by5{}}
rule HLETTER{V_5by5{}}
rule HLETTER{W_5by5{}}
rule HLETTER{X_5by5{}}
rule HLETTER{Y_5by5{}}
rule HLETTER{Z_5by5{}}
rule HLETTER{NUM_1_5by5{}}
rule HLETTER{NUM_2_5by5{}}
rule HLETTER{NUM_3_5by5{}}
rule HLETTER{NUM_4_5by5{}}
rule HLETTER{NUM_5_5by5{}}
rule HLETTER{NUM_6_5by5{}}
rule HLETTER{NUM_7_5by5{}}
rule HLETTER{NUM_8_5by5{}}
rule HLETTER{NUM_9_5by5{}}

