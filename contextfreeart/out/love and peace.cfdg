/* http://strix.org.uk/contextfree/ */

/* uses alphabet by Bargonaut, many thanks to him */

startshape SUN

rule CILIA 100 {  LOVE {r 90} CILIA { x + 1.2  s 0.98 r 6 } }
rule CILIA 100 {  LOVE {r 90} CILIA { x + 1.2  s 0.98 r -6 } } 
rule CILIA 100 {  LOVE {r 90} CILIA { x + 1.2  s 0.98 } }

rule CILIA 100 {  PEACE {r 90} CILIA { x + 1.2  s 0.98 r 6 } }
rule CILIA 100 {  PEACE {r 90} CILIA { x + 1.2  s 0.98 r -5 } } 
rule CILIA 100 {  PEACE {r 90} CILIA { x + 1.2  s 0.98 } }

rule CILIA 1000 { O_BLOCK {} CILIA { x + 1.2  s 0.98 } }

rule CILIA {  CIRCLE {} SUN {} }

rule SUN {
	CILIA { r -90 }
	SUN { x 1.2 r 7 s 0.99}
}

rule LOVE {
	L_BLOCK { x  0}
	O_BLOCK { x 1}
	V_BLOCK { x 2}
	E_BLOCK { x 3}
}

rule PEACE {
	E_BLOCK { x  0}
	C_BLOCK { x -1}
	A_BLOCK { x -2}
	E_BLOCK { x -3}
	P_BLOCK { x -4}
}





rule alphabet {
A_BLOCK	{ x 0 y 0 }
B_BLOCK	{ x 1 y 0 }
C_BLOCK	{ x 2 y 0 }
D_BLOCK	{ x 3 y 0 }
E_BLOCK	{ x 4 y 0 }
F_BLOCK	{ x 5 y 0 }
G_BLOCK	{ x 6 y 0 }
H_BLOCK	{ x 0 y -1.2 }
I_BLOCK	{ x 1 y -1.2 }
J_BLOCK	{ x 2 y -1.2 }
K_BLOCK	{ x 3 y -1.2 }
L_BLOCK	{ x 4 y -1.2 }
M_BLOCK	{ x 5 y -1.2 }
N_BLOCK	{ x 6 y -1.2 }
O_BLOCK	{ x 0 y -2.4 }
P_BLOCK	{ x 1 y -2.4 }
Q_BLOCK	{ x 2 y -2.4 }
R_BLOCK	{ x 3 y -2.4 }
S_BLOCK	{ x 4 y -2.4 }
T_BLOCK	{ x 5 y -2.4 }
U_BLOCK	{ x 6 y -2.4 }
V_BLOCK	{ x 0 y -3.6 }
W_BLOCK	{ x 1 y -3.6 }
X_BLOCK	{ x 2 y -3.6 }
Y_BLOCK	{ x 3 y -3.6 }
Z_BLOCK	{ x 4 y -3.6 }
ONE_BLOCK	{ x 5 y -3.6 }
TWO_BLOCK	{ x 6 y -3.6 }
THREE_BLOCK	{ x 0 y -4.8 }
FOUR_BLOCK	{ x 1 y -4.8 }
FIVE_BLOCK	{ x 2 y -4.8 }
SIX_BLOCK	{ x 3 y -4.8 }
SEVEN_BLOCK	{ x 4 y -4.8 }
EIGHT_BLOCK	{ x 5 y -4.8 }
NINE_BLOCK	{ x 6 y -4.8 }
ZERO_BLOCK	{ x 0 y -6.0 }
ACUTE_BLOCK { x 1 y -6.2 }
GRAVE_BLOCK { x 2 y -6.2 }
LSQUOTE_BLOCK { x 3 y -6 }
RSQUOTE_BLOCK { x 4 y -6 }
LDQUOTE_BLOCK { x 5 y -6 }
RDQUOTE_BLOCK { x 6 y -6 }
SLASH_BLOCK { x 0 y -7.2 }
BACKSLASH_BLOCK { x 1 y -7.2 }
MINUS_BLOCK { x 2 y -7.2 }
PLUS_BLOCK { x 3 y -7.2 }
MULT_BLOCK { x 4 y -7.2 }
DIV_BLOCK { x 5 y -7.2 }
ASTERISK_BLOCK { x 6 y -7.2 }
EQUALS_BLOCK { x 7 y -7.2 }
QUEST_BLOCK { x 7 y 0 }
EXCLAM_BLOCK { x 7 y -1.2 }
PERIOD_BLOCK { x 7 y -3.6 }
COMMA_BLOCK { x 7.5 y -3.6 }
AT_BLOCK { x 7 y -2.4 }
COLON_BLOCK { x 7 y -4.8 }
SEMICOLON_BLOCK { x 7.5 y -4.8 }
TILDA_BLOCK { x 7 y -6.2 }
}

// Alphabet of Block letters

rule A_BLOCK {
SQUARE { s 0.3 0.6 x -0.2 y -0.2
	skew 45.5 0 }
SQUARE { s 0.3 0.6 x 0.2 y -0.2
	skew -45.5 0 }
TRIANGLE { s 0.44 0.5 x 0 y 0.2 }
TRIANGLE	{ s 0.2 0.25 y -0.1 
	sat -1 b 1 }
SQUARE { s 0.5 0.2 x 0 y -0.2 }
}

rule  B_BLOCK {
SQUARE	{ s 0.5 1 x -0.1 }
CIRCLE { s 0.5 0.5 x 0.1 y 0.25  }
CIRCLE { s 0.2 x 0 y 0.25
	sat -1 b 1 }
CIRCLE { s 0.6 0.6 x 0.1 y -0.2  }
CIRCLE { s 0.2 x 0 y -0.2
	sat -1 b 1 }
}

rule C_BLOCK {
CIRCLE { s 0.95 1 }
CIRCLE { s 0.44 b 1 }
SQUARE { s 0.45 0.25 x .25
	sat -1 b 1 }
}

rule D_BLOCK {
SQUARE	{ s 0.45 1 x -0.15 }
CIRCLE { s 0.75 1 x 0.05 }
CIRCLE { s 0.33 0.5 x -0.05
	sat -1 b 1 }
SQUARE { s 0.25 0.8 x -0.2 y 0 }
}

rule E_BLOCK {
SQUARE { s 0.25 1 x -0.275 }
SQUARE { s 0.8 0.2 y 0.4 }
SQUARE { s 0.8 0.2 y -0.4 }
SQUARE	{ s 0.5 0.2 }
}

rule F_BLOCK {
SQUARE { s 0.25 1 x -0.275 }
SQUARE { s 0.8 0.2 y 0.4 }
SQUARE	{ s 0.5 0.2  x 0 }
}

rule G_BLOCK {
CIRCLE { s 0.95 1 }
CIRCLE { s 0.45
	sat -1 b 1 }
SQUARE { s 0.45 0.1 x .25 y 0.1
	sat -1 b 1 }
SQUARE { s 0.45 0.19 x .2 y -0.05 }
}

rule H_BLOCK {
SQUARE { s 0.25 1 x -0.25 }
SQUARE { s 0.25 1 x 0.25 }
SQUARE { s 0.75 0.25 }
}

rule I_BLOCK {
SQUARE { s 0.7 0.2 y 0.4 }
SQUARE { s 0.7 0.2 y -0.4 }
SQUARE { s 0.25 1 }
}

rule J_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y -0.2  }
CIRCLE { s 0.3 0.25 x 0 y -0.15
	sat -1 b 1 }
SQUARE { s 0.75 0.5 x -0.1 y 0.1
	sat -1 b 1 }
SQUARE { s 0.25 .7 x 0.275 y 0.15 }
}

rule K_BLOCK {
SQUARE { s 0.3 1 x -0.3 }
SQUARE { s 0.25 0.5 x 0.1 y 0.24
	skew 45 0 }
SQUARE { s 0.3 0.5 x 0.1 y -0.24
	skew -45 0 }
}

rule L_BLOCK {
SQUARE { s 0.25 1 x -0.28 }
SQUARE { s 0.8 0.2 y -0.4 }
}

rule M_BLOCK {
SQUARE { s 0.25 1 x -0.31 }
SQUARE { s 0.25 1 x 0.31 }
SQUARE { s 0.2 0.5 x -0.16 y 0.25
	skew -40 0 }
SQUARE { s 0.2 0.5 x 0.16 y 0.25
	skew 40 0 }
TRIANGLE { s 0.5 0.6 x 0 y -0.15
	rotate 180 }
}

rule N_BLOCK {
SQUARE { s 0.25 1 x -0.25 }
SQUARE { s 0.25 1 x 0.25 }
SQUARE { s 0.2 1 skew -60 0 }
}

rule O_BLOCK {
CIRCLE { s 0.95 1 }
CIRCLE { s 0.4
	sat -1 b 1 }
}

rule  P_BLOCK {
SQUARE	{ s 0.25 1 x -0.2 }
SQUARE	{ s 0.4 0.6 x -0.1 y 0.2 }
CIRCLE { s 0.5 0.6 x 0.1 y 0.2 }
CIRCLE { s 0.2 x 0.05 y 0.2 
	sat -1 b 1 }
}

rule Q_BLOCK {
CIRCLE { s 0.95 1 }
CIRCLE { s 0.45 
	sat -1 b 1 }
SQUARE { s 0.2 0.5 x 0.2 y -0.25
	skew -60 0 }
}

rule  R_BLOCK {
SQUARE	{ s 0.25 1 x -0.2 }
SQUARE	{ s 0.4 0.5 x -0.1 y 0.25 }
CIRCLE { s 0.5 0.6 x 0.1 y 0.2 }
CIRCLE { s 0.2 x 0 y 0.2
	sat -1 b 1 }
SQUARE { s 0.3 0.5 x 0.1 y -0.25
	skew -45 0 }
}

rule  S_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y 0.2  }
CIRCLE { s 0.2 0.2 x 0 y 0.2
	sat -1 b 1 }
CIRCLE { s 0.8 0.6 x 0 y -0.2  }
CIRCLE { s 0.25 0.2 x 0 y -0.2
	sat -1 b 1 }
SQUARE{ s 0.4 0.25 x 0.2 y 0.1
	sat -1 b 1 }
SQUARE{ s 0.4 0.25 x -0.2 y -0.1
	sat -1 b 1 }
CIRCLE { s 0.45 0.15 x -0.125 y -0.0
	rotate -17 }
CIRCLE { s 0.45 0.15 x 0.125 y -0.0
	rotate -17 }
}

rule T_BLOCK {
SQUARE { s 0.25 .95 x 0 y -0.02 }
SQUARE { s 0.8 0.2 y 0.4 }
}

rule U_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y -0.2  }
CIRCLE { s 0.3 0.25 x 0 y -0.15
	sat -1 b 1 }
SQUARE { s 0.33 0.5 x 0 y 0.1
	sat -1 b 1 }
SQUARE { s 0.25 .7 x -0.275 y 0.15 }
SQUARE { s 0.25 .7 x 0.275 y 0.15 }
}

rule V_BLOCK {
SQUARE { s 0.3 0.6 x -0.2 y 0.2
	skew -45.5 0 }
SQUARE { s 0.3 0.6 x 0.2 y 0.2
	skew 45.5 0 }
TRIANGLE { s 0.44 0.5 x 0 y -0.2
	rotate 180 }
}

rule W_BLOCK {
SQUARE { s 0.25 1 x -0.31 }
SQUARE { s 0.25 1 x 0.31 }
SQUARE { s 0.2 0.5 x -0.17 y -0.25
	skew 42 0 }
SQUARE { s 0.2 0.5 x 0.17 y -0.25
	skew -42 0 }
TRIANGLE { s 0.4 0.4 x 0 y 0.1 }
}

rule X_BLOCK {
SQUARE { s 0.3 1 skew -60 0 }
SQUARE { s 0.3 1 skew 60 0 }
}

rule Y_BLOCK {
SQUARE { s 0.3 0.5 x -0.15 y 0.24
	skew -45 0 }
SQUARE { s 0.3 0.5 x 0.15 y 0.24
	skew 45 0 }
SQUARE { s 0.3 0.5 x 0 y -0.24 }
}

rule Z_BLOCK {
SQUARE { s 0.8 0.25 y 0.37 }
SQUARE { s 0.8 0.25 y -0.37 }
SQUARE { s 0.3 0.5 skew 59.2 0 }
}


rule ONE_BLOCK {
SQUARE { s 0.3 1 }
TRIANGLE { s 0.25 x -0.15 y 0.3
	rotate 0 }
}

rule  TWO_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y 0.2  }
CIRCLE { s 0.25 0.2 x 0 y 0.15
	sat -1 b 1 }
SQUARE{ s 0.4 0.1 x -0.23 y 0.15
	sat -1 b 1 }
SQUARE { s 0.25 0.25 x -0.185 y -0.13
	skew 36 0 }
TRIANGLE { s 0.25 0.53 x -0.24 y 0.065
	rotate -88
	sat -1 b 1 }
CIRCLE { s 0.2 0.3  x -0.265 y -0.14
	rotate -55 }
SQUARE { s 0.3 0.2 x -0.25 y -0.27 }
SQUARE{ s 0.3 0.1 x 0 y -0.08
	rotate 20 }
SQUARE { s 0.8 0.25 y -0.38 }
}

rule  THREE_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y 0.2  }
CIRCLE { s 0.25 0.2 x 0 y 0.2
	sat -1 b 1 }
CIRCLE { s 0.8 0.6 x 0 y -0.2  }
CIRCLE { s 0.25 0.2 x 0 y -0.2
	sat -1 b 1 }
SQUARE{ s 0.36 0.425 x -0.22 y 0
	sat -1 b 1 }
}

rule FOUR_BLOCK {
SQUARE { s 0.2 0.99 x 0.175 y 0 }
SQUARE { s 0.25 0.6 x -0.08 y 0.195
	skew 60 0 }
SQUARE { s 0.75 0.2 x -0.05 y -0.2 }
}

rule  FIVE_BLOCK {
CIRCLE { s 0.8 0.7 x 0 y -0.13 }
CIRCLE { s 0.3 0.3 x 0 y -0.1
	sat -1 b 1 }
SQUARE { s 0.2 0.5 x -0.27 y 0.21 }
SQUARE { s 0.7 0.2 x -0.02 y 0.38 }
SQUARE{ s 0.5 0.2 x -0.2 y -0.1
	sat -1 b 1 }
}

rule  SIX_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y 0.2  }
CIRCLE { s 0.25 0.4 x 0 y 0.1
	sat -1 b 1 }
SQUARE{ s 0.45 0.4 x 0.2 y 0
	sat -1 b 1 }
CIRCLE { s 0.3 0.7 x -0.3 y 0 }
CIRCLE { s 0.8 0.6 x 0 y -0.2  }
CIRCLE { s 0.25 0.25 x 0 y -0.2
	sat -1 b 1 }
}

rule SEVEN_BLOCK {
SQUARE { s 0.8 0.25 y 0.38 }
SQUARE { s 0.3 0.75 x 0.03 y -0.12
	skew 55 0 }
}

rule  EIGHT_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y 0.2  }
CIRCLE { s 0.8 0.6 x 0 y -0.2  }
CIRCLE { s 0.2 0.2 x 0 y 0.2
	sat -1 b 1 }
CIRCLE { s 0.25 0.2 x 0 y -0.2
	sat -1 b 1 }
}

rule  NINE_BLOCK {
CIRCLE { s 0.8 0.6 x 0 y 0.2  }
CIRCLE { s 0.25 0.2 x 0 y 0.2
	sat -1 b 1 }
SQUARE { s 0.25 0.54 x 0 y -0.22
	skew 60 0 }
}

rule ZERO_BLOCK {
CIRCLE { s 0.8 1 }
CIRCLE { s 0.4
	sat -1 b 1 }
SQUARE { s 0.1 0.5 x 0 y 0
	skew 70 0 }
}

rule PERIOD_BLOCK {
CIRCLE { x -0.25 y -0.38 s 0.2 }
}

rule COMMA_BLOCK {
CIRCLE { x -0.25 y -0.38 s 0.18 }
TRIANGLE { x -0.24 y -0.47 s 0.07 0.15
	rotate 155 }
}

rule SLASH_BLOCK {
SQUARE { s 0.2 0.9 x 0 y 0
	skew 60 0 }
}

rule BACKSLASH_BLOCK {
SQUARE { s 0.2 0.9 x 0 y 0
	skew -60 0 }
}

rule COLON_BLOCK {
CIRCLE { x -0.25 y -0.1 s 0.15 }
CIRCLE { x -0.25 y -0.4 s 0.15 }
}

rule SEMICOLON_BLOCK {
CIRCLE { x -0.25 y -0.1 s 0.15 }
CIRCLE { x -0.25 y -0.4 s 0.14 }
TRIANGLE { x -0.25 y -0.47 s 0.07 0.15
	rotate 155 }
}

rule RDQUOTE_BLOCK {
CIRCLE { x -0.25 y 0.3 s 0.14 }
TRIANGLE { x -0.25 y 0.23 s 0.07 0.15
	rotate 155 }
CIRCLE { x -0.07 y 0.3 s 0.14 }
TRIANGLE { x -0.07 y 0.23 s 0.07 0.15
	rotate 155 }
}

rule RSQUOTE_BLOCK {
CIRCLE { x -0.25 y 0.3 s 0.14 }
TRIANGLE { x -0.25 y 0.23 s 0.07 0.15
	rotate 155 }
}

rule LDQUOTE_BLOCK {
CIRCLE { x 0.25 y 0.23 s 0.14 }
TRIANGLE { x 0.25 y 0.3 s 0.07 0.15
	rotate -25 }
CIRCLE { x 0.07 y 0.23 s 0.14 }
TRIANGLE { x 0.07 y 0.3 s 0.07 0.15
	rotate -25 }
}

rule LSQUOTE_BLOCK {
CIRCLE { x 0.25 y 0.23 s 0.14 }
TRIANGLE { x 0.25 y 0.3 s 0.07 0.15
	rotate -25 }
}

rule MINUS_BLOCK {
SQUARE { s 0.5 0.15 x 0 y 0 }
}

rule PLUS_BLOCK {
MINUS_BLOCK { }
SQUARE { s 0.15 0.5 x 0 y 0 }
}

rule MULT_BLOCK {
PLUS_BLOCK { rotate 45 }
}

rule DIV_BLOCK {
MINUS_BLOCK {}
CIRCLE { x 0 y 0.2 s 0.15 }
CIRCLE { x 0 y -0.2 s 0.15 }
}

rule ASTERISK_BLOCK {
SQUARE { s 0.1 0.5 }
SQUARE { s 0.1 0.5 rotate 60 }
SQUARE { s 0.1 0.5 rotate -60 }
}

rule EQUALS_BLOCK {
SQUARE { s 0.5 0.15 x 0 y 0.125 }
SQUARE { s 0.5 0.15 x 0 y -0.125 }
}

rule EXCLAM_BLOCK {
TRIANGLE { s 0.07 0.66 x -0.0725 y 0.24 
	rotate 180 skew -30 0 }
TRIANGLE { s 0.07 0.66 x 0.0725 y 0.24 
	rotate 180 skew 30 0 }
SQUARE { s 0.1 0.6 y 0.13 }
CIRCLE { s 0.2 y -0.36 }
}

rule QUEST_BLOCK {
CIRCLE { s 0.8 0.5 x 0 y 0.2  }
CIRCLE { s 0.25 0.2 x 0 y 0.2
	sat -1 b 1 }
SQUARE{ s 0.36 0.425 x -0.22 y 0
	sat -1 b 1 }
SQUARE { s 0.15 0.3 y -0.05 }
TRIANGLE { s 0.2 0.1 x -0.07 y .07
	rotate 45
	sat -1 b 1 }
CIRCLE { s 0.2 y -0.36 }
}

rule ACUTE_BLOCK {
TRIANGLE { x 0 y 0.5 s 0.05 0.2
	rotate 125 }
}

rule GRAVE_BLOCK {
TRIANGLE { x 0 y 0.5 s 0.05 0.2
	rotate -125 }
}

rule _hump_block {
CIRCLE { }
_hump_block { s 0.993 x 0.06 y 0.005 rotate 0.6667 }
}

rule TILDA_BLOCK {
_hump_block { s 0.05 y 0.38 rotate -45 }
_hump_block { s 0.05 y 0.38 rotate 135 }
}

rule _spiral_block {
CIRCLE {}
_spiral_block { s 0.985 x -0.1 y -0.1
	r -5 }
}
rule AT_BLOCK {
_spiral_block {  x 0.14 y -0.34 s 0.33 
	r -45 }
}
