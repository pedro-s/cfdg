// stealing an interesting idea
// http://contextfreeart.org/gallery/view.php?id=3576

startshape Canvas // Render (800*600)
WIDTH = 800
HEIGHT = 600

CF::Background=[b -1]
CF::ColorDepth=16
CF::Size=[s WIDTH HEIGHT]
CF::Impure=1

STEP=0.5
//STEP=4

G=(1+sqrt(5))/2
number Saw(X,S)=X-S*(0.5+floor(X/S))

number Land1(X,Y)=(sin(X)+sin(Y))*0.3333333333
number Land(X,Y)=(sin(Land1(X,Y)*31)+sin(Land1(X*1.3+88,Y)*31+57))*0.5

tt = 55.5 //rand_static(2000)
dx = (tt+100)/1.2
dy = tt+100

number Land2(X,Y)= let(
  rgb = Landscape(0, X/WIDTH+dx, Y/HEIGHT+dy);
  (rgb[0]+rgb[1]+rgb[2])/2
)

shape Canvas
{ 
  loop Y=-WIDTH,WIDTH,STEP []
  loop X=-HEIGHT,HEIGHT,STEP []
  {
    S1=Land(X,Y)
    S2=Land2(X/2-600,Y/2+790)
    S =520+(S1+S2*1.5)*50
    B =350+Land2(X*1.2+145,Y-66)*120  
    Dx=Saw(2*X+cos(Y*360/S+56)/G*S,S)
    Dy=Saw(2*Y-sin(X*360/S-08)/G*S,S)
    R =sqrt(Dx^2+Dy^2)/S-0.5
    L1= (S1*1.5)^4
    L2= (S2/1.75)^4+0.125
    L =(L1*(0.5+L2)+L2)*0.5
    if (R<-0.05)
      Dot(S-1,X,Y,B)[h 20 b (L*1+0.5) sat (.4-L*.15) z -1]
    else 
      SQUARE [s STEP x X Y h (45-L*30) b (L*1+0.6) sat (0.9-sqrt(L))]
  }
}

shape Dot(S,X,Y,B)
{ 
  if(S>B)
  { 
    Dx=Saw(2*X+cos(Y*360/S+56)/G*S,S)
    Dy=Saw(2*Y-sin(X*360/S-08)/G*S,S)
    R =sqrt(Dx^2+Dy^2)/S-0.5
    if (R<-0.05) 
      Dot(S-1,X,Y,B)[h -0.05 b -0.0125 sat 0.01 z -1]
    else if (R<0.01)  
      SQUARE [s STEP x X Y h ((S-B)*0.1)] 
  } else { 
    L = (B/500)^4
    SQUARE [s STEP x X Y h (83+L*20-B*0.2) b (L*0.5-0.25) sat (1.85-L) ]
  }
}

vector3 Landscape(i, number x0, number y0) = if(
  i<5, 
 
  let(
    x1 = (x0+rcos(tt+y0))*1.25;
    y1 = (y0+rsin(tt+x0))*1.25;
    rgb = Landscape(i+1, x1*2, y1*2); 
    fk = abs(x1/y1/3);
    rgb[0]+fk*rsin(x1), 
    rgb[1]+fk*rcos(y1), 
    rgb[2]+fk*rcos(x1)
  ),
 
  (0.0, 0.85, 1.4)
)

rsin(radian) = sin(radian*180/3.14)
rcos(radian) = cos(radian*180/3.14)
